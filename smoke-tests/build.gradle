import jdk.internal.joptsimple.internal.Strings

// TODO : convert to kotlin for consistency
plugins {
  id "java"
  alias catalog.plugins.taskinfo
}

configurations {
  warOnly {
    transitive = false
    canBeResolved = true
    canBeConsumed = false
  }
}

dependencies {
  testImplementation("org.testcontainers:testcontainers:1.19.1")
  testImplementation("com.fasterxml.jackson.core:jackson-databind:2.15.2")
  testImplementation("com.google.protobuf:protobuf-java-util:3.24.2")
  testImplementation("com.squareup.okhttp3:okhttp:4.11.0")
  testImplementation("io.opentelemetry.proto:opentelemetry-proto:1.0.0-alpha")
  testImplementation("io.opentelemetry:opentelemetry-api")

  testImplementation("ch.qos.logback:logback-classic:1.4.11")

  testImplementation("org.assertj:assertj-core:3.24.2")

  testImplementation("io.opentelemetry.semconv:opentelemetry-semconv")

  testImplementation("org.mock-server:mockserver-client-java:5.15.0")

  warOnly(project("test-app-war")){
    attributes {
        attribute(Attribute.of("elastic.packaging", String.class), "war")
      }
  }
}

tasks.test {
  useJUnitPlatform()

  testLogging.showStandardStreams = true

  // ensure test application docker image is available locally with 'latest' tag
  dependsOn(":smoke-tests:test-app:assemble")

  // depends on packaged agent output file
  def agentShadowTask = project(":agent").tasks.shadowJar
  dependsOn(agentShadowTask)
  inputs.files(layout.files(agentShadowTask))

  // depends on the packaged test app war file
  dependsOn(project(":smoke-tests:test-app-war").tasks.assemble)
  def warFile = project.configurations.warOnly.files.stream().findFirst().get()
  inputs.files(layout.files(warFile))

  doFirst {
    jvmArgs(
        "-Dio.opentelemetry.smoketest.agent.shadowJar.path=${agentShadowTask.archiveFile.get()}",
        "-Dio.opentelemetry.smoketest.agent.testAppWar.path=${warFile}")
  }
}
